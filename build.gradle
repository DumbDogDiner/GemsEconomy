import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "kr.entree.spigradle" version "2.0.0"
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

group "com.dumbdogdiner"
version "2.0.0"

sourceCompatibility = 13
targetCompatibility = 13

repositories {
    mavenCentral()
    spigot()
    jcenter()
    codemc()
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://oss.sonatype.org/content/repositories/central" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi" }
    maven { url = "https://raw.githubusercontent.com/JorelAli/1.13-Command-API/mvn-repo/1.13CommandAPI/" }
    jitpack()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7")
    implementation("dev.jorel:commandapi-core:3.2")

    compileOnly(paper("1.16.1"))
    compileOnly(vault())
    compileOnly("me.clip:placeholderapi:2.10.6")

    compile("org.jetbrains.exposed:exposed-core:0.26.1")
    compile("org.postgresql:postgresql:42.2.2")

//    TODO: remove this
    compile("com.zaxxer:HikariCP:3.4.2")
}

compileKotlin {
    kotlinOptions.jvmTarget = "13"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "13"
}

tasks.build.dependsOn tasks.shadowJar
shadowJar {
    classifier = ""
    project.configurations.implementation.canBeResolved = true
    project.configurations.compile.canBeResolved = true
    configurations = [project.configurations.implementation, project.configurations.compile]
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = project.name
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

spigot {
    authors = List.of("Vlad Frangu")
    softDepends = List.of("Vault", "PlaceholderAPI")
    apiVersion = "1.16"
    load = STARTUP
    loadBefore = List.of("ItemFrameShops")
    commands {
        create("balance") {
            aliases = List.of("gbal", "gmoney", "bal")
            description = "Shows your balances"
            usage = "/gbalance [player]"
        }
        create("baltop") {
            aliases = List.of("gmoneytop", "gecotop", "gtop", "gbaltop")
            description = "Shows the top balances of the server"
            usage = "/baltop [currency] [page]"
        }
        create("currency") {
            aliases = List.of("gcurr", "gcurrency", "currencies")
            description = "Manage the currencies."
            usage = "/gcurrencies <backend|color|colorlist|convert|create|decimals|default|delete|list|payable|setrate|startbal|symbol|view>"
        }
        create("economy") {
            aliases = List.of("geconomy", "eco", "geco", "gemseconomy")
            description = "Give, set or take currency."
            usage = "/eco <add|give|remove|set|take> <account> <amount> [currency]"
        }
        create("pay") {
            aliases = List.of("gpay")
            description = "Pays another account"
            usage = "/pay <account> <amount> [currency]"
        }
        create("check") {
            aliases = List.of("note", "banknote")
            description = "Write or Redeem a check"
            usage = "/check <redeem|write> [amount] [currency]"
        }
//        create("exchange") {
//            aliases = List.of("gex", "ex")
//            description = "Exchange currencies"
//            usage = "/exchange [player] <old currency> <amount> <new currency> [receive amount]"
//        }
    }
}

apply plugin: "eclipse"

// Eclipse classpath bugfix
eclipse {
   classpath {
        containers "org.eclipse.buildship.core.gradleclasspathcontainer"
   }
}
